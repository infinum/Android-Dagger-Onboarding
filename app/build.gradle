apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'scabbard.gradle'

scabbard {
    enabled true
    outputFormat "svg"
}

android {
    compileSdk 34
    namespace "com.infinum.daggeronboarding"

    defaultConfig {
        applicationId "com.indinum.daggeronboarding"
        minSdkVersion 24
        targetSdkVersion 34
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

}

configurations.all {
    resolutionStrategy.eachDependency {
        if (requested.group == "com.github.kittinunf.result" && requested.name == "result" && requested.version == "3.0.0") {
            useVersion("3.0.1")
            because("Transitive dependency of Scabbard, currently not available on mavenCentral()")
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation libs.kotlin
    implementation libs.appCompat
    implementation libs.coreKtx
    implementation libs.constraintLayout

    // dependency injection libs
    implementation libs.dagger
    kapt libs.dagger.compiler
    kapt libs.dagger.android.processor
    implementation libs.dagger.android.support

    testImplementation libs.junit
    androidTestImplementation libs.junit.androidx
    androidTestImplementation libs.espresso
}
